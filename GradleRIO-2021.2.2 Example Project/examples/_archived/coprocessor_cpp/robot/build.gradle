apply plugin: "edu.wpi.first.GradleRIO"

deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            team = 5333
        }
    }
    artifacts {
        // Define my robot artifact. The FRCNativeArtifact type gives access to
        // robotCommand and other tools necessary for the RoboRIO to detect
        // and run our code.
        artifact('robotCode', edu.wpi.first.gradlerio.frc.FRCNativeArtifact) {
            targets << "roborio"
            component = 'frcUserProgram'
        }
    }
}

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            // Target the RoboRIO (arm-frc2019-linux-gnueabihf)
            targetPlatform 'roborio'
            sources.cpp {
                // This is where our source files are located (src/cpp for C++ files, src/include for headers)
                source {
                    srcDir 'src/cpp'
                    include '**/*.cpp'
                }
                exportedHeaders {
                    srcDir 'src/include'
                    include '**/*.hpp', '**/*.h'
                }
            }
            useLibrary(it, "wpilib", "ctre", "navx")
        }
    }
}
